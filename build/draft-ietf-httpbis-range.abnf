
  range-unit       = bytes-unit / other-range-unit

  bytes-unit       = "bytes"

  byte-ranges-specifier = bytes-unit "=" byte-range-set
  byte-range-set  = 1#( byte-range-spec / suffix-byte-range-spec )
  byte-range-spec = first-byte-pos "-" [ last-byte-pos ]
  first-byte-pos  = 1*DIGIT
  last-byte-pos   = 1*DIGIT

  suffix-byte-range-spec = "-" suffix-length
  suffix-length = 1*DIGIT

  other-range-unit = token

  Accept-Ranges     = acceptable-ranges
  acceptable-ranges = 1#range-unit / "none"

  Range = byte-ranges-specifier / other-ranges-specifier
  other-ranges-specifier = other-range-unit "=" other-range-set
  other-range-set = 1*VCHAR

  If-Range = entity-tag / HTTP-date

  Content-Range       = byte-content-range
                      / other-content-range
                          
  byte-content-range  = bytes-unit SP
                        ( byte-range-resp / unsatisfied-range )

  byte-range-resp     = byte-range "/" ( complete-length / "*" )
  byte-range          = first-byte-pos "-" last-byte-pos
  unsatisfied-range   = "*/" complete-length
                          
  complete-length     = 1*DIGIT
  
  other-content-range = other-range-unit SP other-range-resp
  other-range-resp    = *CHAR

  OWS        = <OWS, see [SEMNTCS], Section 2.7>
  token      = <token, see [MESSGNG], Section 2.2.5>

  HTTP-date  = <HTTP-date, see [SEMNTCS], Section 8.1.1.1>
  entity-tag = <entity-tag, see [CONDTNL], Section 2.3>
