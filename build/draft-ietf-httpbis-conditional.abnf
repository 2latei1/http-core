
  Last-Modified = HTTP-date

  ETag       = entity-tag

  entity-tag = [ weak ] opaque-tag
  weak       = %x57.2F ; "W/", case-sensitive
  opaque-tag = DQUOTE *etagc DQUOTE
  etagc      = %x21 / %x23-7E / obs-text
             ; VCHAR except double quotes, plus obs-text

  If-Match = "*" / 1#entity-tag

  If-None-Match = "*" / 1#entity-tag

  If-Modified-Since = HTTP-date

  If-Unmodified-Since = HTTP-date

  OWS           = <OWS, see [SEMNTCS], Section 2.7>
  obs-text      = <obs-text, see [MESSGNG], Section 2.2.5>

  HTTP-date     = <HTTP-date, see [SEMNTCS], Section 8.1.1.1>
