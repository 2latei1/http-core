
  delta-seconds  = 1*DIGIT

  Age = delta-seconds

  Cache-Control   = 1#cache-directive

  cache-directive = token [ "=" ( token / quoted-string ) ]

  Expires = HTTP-date

  Pragma           = 1#pragma-directive
  pragma-directive = "no-cache" / extension-pragma
  extension-pragma = token [ "=" ( token / quoted-string ) ]

  Warning       = 1#warning-value

  warning-value = warn-code SP warn-agent SP warn-text
                                        [ SP warn-date ]

  warn-code  = 3DIGIT
  warn-agent = ( uri-host [ ":" port ] ) / pseudonym
                  ; the name or pseudonym of the server adding
                  ; the Warning header field, for use in debugging
                  ; a single "-" is recommended when agent unknown
  warn-text  = quoted-string
  warn-date  = DQUOTE HTTP-date DQUOTE

  OWS           = <OWS, see [SEMNTCS], Section 2.7>
  field-name    = <field-name, see [MESSGNG], Section 2.2>
  quoted-string = <quoted-string, see [MESSGNG], Section 2.2.5>
  token         = <token, see [MESSGNG], Section 2.2.5>

  port          = <port, see [SEMNTCS], Section 2.8>
  pseudonym     = <pseudonym, see [MESSGNG], Section 4.7.1>
  uri-host      = <uri-host, see [SEMNTCS], Section 2.8>

  HTTP-date     = <HTTP-date, see [SEMNTCS], Section 8.1.1.1>
